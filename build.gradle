group 'catumbre'
version '1.0-SNAPSHOT'

apply plugin: 'java'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	jcenter()
}

dependencies {
	compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.12'

	compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.3'
	compile group: 'net.java.dev.jna', name: 'jna', version: '4.3.0'
	compile group: 'com.github.bdeneuter', name: 'dbus-java', version: '2.7'

	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.22'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
	compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.7'

	runtime fileTree(dir: 'lib', include: ['*.so'])
}

jar {
	manifest {
		attributes(
				'Main-Class': 'org.rl.scheduled.turnoff.MainController',
				'Class-Path': '. ' + configurations.compile.collect({ it.name }).join(' ')
		)
	}
}

task libunixGet {
	def soFile = 'libunix-java.so'
	def libDir = new File(projectDir, 'lib')
	def absoluteSoFile = new File(libDir, soFile)

	outputs.dir libDir
	outputs.file absoluteSoFile

	doLast {
		libDir.mkdirs()
		ant.get(src: 'http://central.maven.org/maven2/org/abstractj/libmatthew/libunix/0.5.1/libunix-0.5.1.so',
				dest: absoluteSoFile,
				verbose: true)
	}
}

task deploy {
	dependsOn build
	dependsOn libunixGet

	doLast {
		//Using ant over gradle Sync task, as it is lot quicker
		ant.sync(toDir: 'Z:\\Projects\\scheduler') {
			ant.file(file: jar.archivePath)
			ant.fileset(dir: 'src/dist/config')

			(configurations.runtime + configurations.compile).each {
				ant.file(file: it)
			}
		}
	}
}